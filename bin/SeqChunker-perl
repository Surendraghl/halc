#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;

use FindBin qw($RealBin $Script);

my $version = "0.22.2";

# Variable defaults
my $_debug=0;
my $help=0;
my $man=0;
my $version_requested=0;

my $CHUNK_NUM=0;

my $CHUNK_SIZE=0;
my $CHUNK_FIRST=1;
my $CHUNK_LAST=0;
my $CHUNK_STEP=1;
my $CHUNK_STEP_NUM=1;

my $OUT="";
my $OUT_SPLIT=0;
my $MAX_BLOCK_SIZE=1000000;

my $cache = "";

my $PATTERN;
my $interleaved;
my $paired;
my $percentage;

=pod

=head1 NAME

SeqChunker - split a fasta/fastq file into chunk of a specified size on the fly

=head1 SYNOPSIS

  SeqChunker --chunk-number|--chunk-size INT [OPTIONS...] FILE(S)";

=head1 DESCRIPTION

SeqChunker efficiently samples and outputs chunks from FASTA and FASTQ files,
 e.g. to serve as on-the-fly input for other tools.

=head1 ARGUMENTS

Required: Either one of the following, but not both at the same time. The unset
 parameter is computed respectively.

  -n/--chunk-number       Total number of chunks to be sampled (including
                          skipped ones).
  -s/--chunk-size         Size of chunks to be sampled. Supports suffixes "k,M,G".

=head2 NOTE

Chunk sizes need to be at least twice as great as the longest record
in the file. Otherwise results will be inconsistent.

Chunk related computations are run on each input file individually.

=head1 OPTIONS

  -f/--first-chunk        Skip chunks before this chunk
  -l/--last-chunk         Last after this chunk (including skipped ones)
  -x/--chunk-step         Output a chunk every -x chunks
  -y/--chunks-per-step    Output -y chunks every -x chunks. Cannot be greater
                          than -x
  -p/--percentage         Output this percentage of data, is internally transformed
                          into adequate -x/-y ratio. 0.1 % is the smallest unit.
  -o/--out                Output filename. To split chunks into individual files
                          provide a "printf" style pattern, e.g. "chunk%02d.fa",
                          with a substitution for the chunk counter.
  -i/--interleaved        Input data is interleaved FASTQ - keep pairs intact.
  -p/--paired             Input files are paired-end libraries in alternating order.
  -m/--max-block-size     Maximum size of blocks in output stream [default 1000000 bytes]
  -q/--quiet              Suppress non-critical messages
  -V/--version            Version of the script.
  -d/--debug              Output more verbose messages

  -h/--help               Show this help screen
  -m/--manual             Show the complete manual page

=head2 EXAMPLES

  # output only even numbered chunks
  SeqChunker --chunk-size 5M --chunk-first 2 --chunk-step 2 my.fa

  # Split file in 100 individual files of similar size
  SeqChunker --chunk-number 100 --out "my.%03d.fa" my.fa

=head1 TODO

=head2 BUGS

=over 8

=item B<Output differences to SeqChunker-dd>

Currently it seems that in small chunk sizes sometimes result in
different output compared to the old SeqChunker-dd

It is still not clear was causes this behavior!

Solved in version 0.20.1

Bug was caused by the security margin. This can be removed in future versions.

=item B<Missing features>

=over

=item * Currently the option C<< -quiet >> is not implemented

=item * The usage of the parameter C<< max_blocksize >> is not implemented

=back

=back

=head2 PLANNED FEATURES

=over 8

=item rand

if possible with seed to allow for consistent recomputation

=back

=head1 HISTORY

=over 8

=item version 0.22.2

--percentage can be provided as a more intuitive sampling value. It
will be transformed into an adequate -x/-y ratio.

=item version 0.22.1

--paired to support sampling for paired files. Uses interleaved
intermediate to ensure proper pair handling. This however, largely
reduces performance compared to regular mode.

=item version 0.21.1

--interleaved to support for interleaved files, respecting paired read
record boundaries.

Support reading form STDIN.

=item version 0.20.3

Fixed another issue with the evaluation of the last-chunk
parameter.

Added output for the version parameter.

Added a section for parameter manual to the documentation

=item version 0.20.2

Fixed an issue with the evaluation of the last-chunk parameter. Tests
are now passed also with SeqChunker-perl

=item version 0.20.1

Solved the differences between old SeqChunker-dd and SeqChunker-perl
under some parameter settings. New perl implementation should now
return exact the same chunks.

=item version 0.20.0

First implementation of a perl based SeqChunker to circumvent some
bash/sed/math issues in the script version. Is mainly based on
SeqChunker-dd version 0.14 and give same result for larger chunk sizes.

=back

=head1 AUTHOR

Please report bugs to

Frank Foerster E<lt>frank.foerster@biozentrum.uni-wuerzburg.deE<gt>

Thomas Hackl E<lt>thomas.hackl@uni-wuerzburg.deE<gt>

Based on code B<SeqChunker.chunk> written by Thomas Hackl

=cut



GetOptions(
    'chunk-number|n=i'    => \$CHUNK_NUM,
    'chunk-size|s=s'      => \$CHUNK_SIZE,
    'first-chunk|f=i'     => \$CHUNK_FIRST,
    'last-chunk|l=i'      => \$CHUNK_LAST,
    'chunk-step|x=i'      => \$CHUNK_STEP,
    'chunks-per-step|y=i' => \$CHUNK_STEP_NUM,
    'percentage|p=f'      => \$percentage,
    'interleaved|i!'      => \$interleaved,
    'paired|2!'           => \$paired,
    'debug|d!'            => \$_debug,
    'help|?|h'            => \$help,
    'out|o=s'             => \$OUT,
    'manual|m'            => \$man,
    'version|V'           => \$version_requested
    ) || pod2usage(2);

if ($version_requested)
{
    print "SeqChunker v",$version,"\n";
    exit;
}

pod2usage(1) if $help;
pod2usage(-exitval => 0, -verbose => 2) if $man;

## Expand the chunk size if required
$CHUNK_SIZE=expand_byte_suffix($CHUNK_SIZE);

## if out contains chunk number substitution set out_split
if($paired){
    unless($OUT){
        print STDERR "--output required for --paired mode";
        exit 2;
    };
    unless($OUT =~ tr/%//){
        print STDERR "--output requires %d pattern for 1/2 in --paired mode\n";
        exit 2;
    };

    if($OUT =~ tr/%// == 2){
        if($percentage){
            print STDERR "in --percentage mode, --output must not have %d chunk-pattern as splitting isn't supported.\n";
            exit 2;
        }
        
        $OUT_SPLIT = 1;
    }
}else{
    if($OUT =~ tr/%//){
        if($percentage){
            print STDERR "--output must not have %d chunk-pattern for splitting in --percentage mode.\n  --percentage doesn't support splitting.\n";
            exit 2;
        }
        
        $OUT_SPLIT=1;
        
    };
}

## the user needs to specify chunk size or chunk num
unless ($percentage || ($CHUNK_SIZE > 0 && $CHUNK_NUM == 0) || ($CHUNK_SIZE == 0 && $CHUNK_NUM > 0))
{
    pod2usage(
              -msg => "Either --chunk-size or --chunk-num required",
              -exitval => 2
             );
}

if($percentage){
    if($CHUNK_FIRST != 1 || $CHUNK_LAST ||  $CHUNK_STEP != 1 || $CHUNK_STEP_NUM != 1 ){
        print STDERR "Ignoring --chunk-step/-step-num/-first/-last in --percentage mode\n";
    }

    # percentage to -x/-y ratio
    $CHUNK_STEP = 1000;
    $CHUNK_STEP_NUM = int(($percentage*10)+.5);
    $CHUNK_NUM = 10000 unless $CHUNK_NUM || $CHUNK_SIZE;
}

## if the user specified chunk last, this value has to be greater or equal to chunk first
if ($CHUNK_LAST != 0 && $CHUNK_FIRST > $CHUNK_LAST)
{
    pod2usage(
              -msg => "--last-chunk cannot be smaller then --first-chunk",
              -exitval => 2
             );
}

## if the user specified chunk step and chunk step num than this value has to be less or equal chunk_step
unless ($CHUNK_STEP_NUM != 0 && $CHUNK_STEP != 0 && $CHUNK_STEP >= $CHUNK_STEP_NUM)
{
    pod2usage(
              -msg => "--chunk-per-step cannot be greater than --chunk-step",
              -exitval => 2
             );
}

if($CHUNK_NUM && $CHUNK_STEP > $CHUNK_NUM){
    print STDERR "--chunk-step cannot be greater than --chunk-num \n";
    exit 2;
}
## if paired mode, files are read as interleaved
$interleaved++ if $paired;

##----------------------------------------------------------------------------##


main_loop(@ARGV);


##----------------------------------------------------------------------------##

=head2 main_loop

=cut

sub main_loop
{
    my @files = @_;

    # generate the filehandle for the output
    my $output_fh = undef;
    my $output_filename = undef;
    my $output_filename2 = undef;

    # if we are not splitting, we need a single output filehandle for
    # everything
    if ($OUT && ! $OUT_SPLIT) {
        if ($paired) {
            $output_filename = sprintf($OUT, 1);
            $output_filename2 = sprintf($OUT, 2);
                    
            $_debug && printf STDERR "The next filenames for output are '%s', '%s'\n", $output_filename, $output_filename2;
                    
            open($output_fh, "| $RealBin/interleaved-split 1>$output_filename 2>$output_filename2")
                || die "Unable to open files '$output_filename', '$output_filename2' for writing! $!\n";
        } else {
                    
            $output_filename = $OUT;
            $_debug && printf STDERR "The next filename for output is '%s'\n", $output_filename;
                    
            open($output_fh, ">", $output_filename)
                || die "Unable to open file '$output_filename' for writing! $!\n";
        }
    } else {
        if ($paired) {
            open($output_fh, "| $RealBin/interleaved-split")
                || die "Unable to open files '$output_filename', '$output_filename2' for writing! $!\n";

        } else {
            $output_fh = *STDOUT;
        }
    }



    for(my $i=0; $i<@files;$i++){
        my $act_file = $files[$i];
        my $fh;
        my $filesize;

        if($paired){
            $i++;
            my $act_file2 = $files[$i];

            if ( $act_file eq '-' or -p $act_file or -t $act_file2
                 or
                 $act_file2 eq '-' or -p $act_file2 or -t $act_file2
               ) {
                die "Reading STDIN is not supported in --paired mode";
            } else {
                # check if the file exists
                unless (-e $act_file){
                    die "The file '$act_file' does not exist!\n";
                }
                unless (-e $act_file2){
                    die "The file '$act_file2' does not exist!\n";
                }

                # get the filesize and open the file
                $filesize = (stat($act_file))[7] + (stat($act_file2))[7];

                if ($_debug) {
                    printf STDERR "Filesize for files '%s' and '%s' is %d bytes!\n", $act_file, $act_file2, $filesize;
                }
            
                unless($filesize){
                    print STDERR "Skipping empty files: $act_file, $act_file2\n";
                    next;
                }
                print STDERR "Reading paired files $act_file, $act_file2\n";
            }

            # create a filehandle to a intermediate interleaved pipe from the paired files
            open($fh, "$RealBin/interleave $act_file $act_file2 |") or die "$!: $act_file, $act_file2";
            
        }else{

            # file or stdin
            if ( $act_file eq '-' or -p $act_file or -t $act_file) {
                $filesize = 0;
                print STDERR "Reading STDIN\n";
                unless($CHUNK_SIZE){
                    print STDERR "Cannot compute --chunk-size on STDIN, using 10000.\n  Decrease on small files or increase for long sequences.\n";
                    $CHUNK_SIZE = 10000;
                    $CHUNK_NUM = 0; # reset chunk_num, doesn't work on stdin
                }
            } else {
                # check if the file exists
                unless (-e $act_file){
                    die "The file '$act_file' does not exist!\n";
                }
                # get the filesize and open the file
                $filesize = (stat($act_file))[7];

                if ($_debug) {
                    printf STDERR "Filesize for file '%s' is %d bytes!\n", $act_file, $filesize;
                }
            
                unless($filesize){
                    print STDERR "Skipping empty file: $act_file\n";
                    next;
                }
                print STDERR "Reading $act_file\n";
            }

            if ($act_file ne '-') {
                open($fh, "<", $act_file) || die "Unable to open file '$act_file' for reading\n";
            } else {
                open($fh, "<&=STDIN") || die "Unable to open file 'STDIN' for reading\n";
            }
        }



        my $fileformat=guess_file_format($fh) || die "The file '$act_file' seems to be neigher a FASTQ nor a FASTA file\n";

        # TODO: throw interleaved + fasta

        if($fileformat eq 'fasta' && $interleaved){
            die "Interleaved FASTA isnt supported"
        }
        
        if ($fileformat eq "fasta"){
            $PATTERN = qr/^>|(?<=\n)>/;
        } elsif ($fileformat eq "fastq"){
            if( ! $interleaved){
                $PATTERN = qr/^@.+\n.+\n\+|(?<=\n)@.+\n.+\n\+/;
            }else{
                $PATTERN = qr/(?:^|(?<=\n))                        # start at new line
                              (@\S+?)(?:\/[12])?(?:[^\S\n].*)?\n   # seq_head: @+id + (picard) + (desc)
                              .+\n                                 # seq
                              \+.*\n                               # qual head
                              .+\n                                 # qual
                              (@\S+?)(?:\/[12])?(?:[^\S\n].*)?\n   # seq_head: @+id + (picard) + (desc)
                              .+\n                                 # seq
                              \+                                   # qual head
                             /x;  
            }
        } else {
            die "Unable to handle the file format '$fileformat'\n";
        }
        
        
	# calculate the chunk size and the number of chunks expected
	if ($CHUNK_SIZE == 0)
	{
	    $CHUNK_SIZE=int($filesize/$CHUNK_NUM)+1;
	} else {
            $CHUNK_NUM=int($filesize/$CHUNK_SIZE)+1;
	}

	# set chunk last if not set
	if ($CHUNK_LAST == 0)
	{
	    $CHUNK_LAST=$CHUNK_NUM;
	}

	# print debug information
	if ($_debug)
	{
	    printf STDERR "ARGV=%s\nCHUNK_SIZE=%d\nCHUNK_NUM=%d\nCHUNK_FIRST=%d\nCHUNK_LAST=%d\nCHUNK_STEP=%d\nCHUNK_STEP_NUM=%d\n", join(",", @_), $CHUNK_SIZE, $CHUNK_NUM, $CHUNK_FIRST, $CHUNK_LAST, $CHUNK_STEP, $CHUNK_STEP_NUM;
	}


        # skip pre --first chunks immediately
        if ($CHUNK_FIRST > 1){
            my $first_skip = ($CHUNK_FIRST-1) * $CHUNK_SIZE;
            if(-p $fh){ # cannot seek on pipe
                my $idontcareabout;
                read($fh, $idontcareabout, $first_skip-tell($fh)-1);
            }else{
                seek($fh, $first_skip, 0);
            }
            $cache = ""; # remove guess format stuff
        }

        
	# counter for the current chunk
	my $current_chunk=$CHUNK_FIRST;

	# this variable determines if a block should be skipped (=1) or kept (=0)
	my $skip = 1;

        # process all chunks until LAST_CHUNK is reached
	while (!$filesize || $current_chunk<=$CHUNK_LAST)
	{

	    # moreover if not every chunk should be returned, we have to determine if this chunk should be returned or not
	    if ((($current_chunk-$CHUNK_FIRST)%$CHUNK_STEP) < $CHUNK_STEP_NUM)
	    {
		$skip = 0;
	    } else {
		$skip = 1;
	    }

	    if ($OUT_SPLIT && $skip == 0)
	    {
                if($paired){
                    $output_filename = sprintf($OUT, $current_chunk, 1);
                    $output_filename2 = sprintf($OUT, $current_chunk, 2);
                    
                    $_debug && printf STDERR "The next filenames for output are '%s', '%s'\n", $output_filename, $output_filename2;
                    
                    open($output_fh, "| $RealBin/interleaved-split 1>$output_filename 2>$output_filename2")
                        || die "Unable to open files '$output_filename', '$output_filename2' for writing! $!\n";
                }else{
                    
                    $output_filename = sprintf($OUT, $current_chunk);
                    $_debug && printf STDERR "The next filename for output is '%s'\n", $output_filename;
                    
                    open($output_fh, ">", $output_filename)
                        || die "Unable to open file '$output_filename' for writing! $!\n";
                }
	    }

	    get_chunk($fh, $current_chunk, $output_fh, $fileformat, $skip);

	    # close the file if a new one was created
	    if ($OUT_SPLIT && $skip == 0)
	    {
		close($output_fh) || die "Unable to open file '$output_filename' after writing! $!\n";
	    }

	    $current_chunk++;

	    # check if the whole file was read
	    if (eof($fh))
	    {
                last;
	    }
	}

        # interleave returns with exits status != 0 because its output
        # might not be read until the end. But in this case, $! is still unset.
	close($fh) || ($! && die "Unable to close file '$act_file' after reading\n");
    }
}

##----------------------------------------------------------------------------##

sub expand_byte_suffix
{
    my ($value) = @_;

    if ($value =~ /^\d+$/)
    {
	return $value+0;
    } elsif  ($value =~ /^(\d+)\s*([kmg])$/i)
    {
	my ($base, $suffix) = ($1, $2);
	return $base*1024**(index("kmg", lc($suffix))+1);
    } else {
	die "The value '$value' does not represent a number or a suffixed number!\n";
    }
}

sub guess_file_format
{
    my ($fh) = @_;

    # read the first byte of the file
    my $first_byte = "";
    my $bytes_received=read $fh, $first_byte, 1;
    if ($bytes_received != 1)
    {
	die "Error on retriving first byte from file: $!\n";
    }

    # check the first byte for indicating a fasta (>), fastq (@) or other file ([^>@])

    if ($first_byte eq "@")
    {
	$cache = $first_byte;
	return "fastq";
    } elsif ($first_byte eq ">")
    {
	$cache = $first_byte;
	return "fasta";
    } else {
	return undef;
    }

}

sub strip_to_blockstart
{
    my ( $ref_string ) = @_;

    if ($$ref_string =~ $PATTERN)
    {
        my $pos;
        unless($interleaved){
            $pos = $-[0];
        }else{
            $_debug && print STDERR $1 eq $2 ? "pair:  $1 $2\n" : "split: $1 $2\n$&\n"; 
            # compare ids, if identical, take both (/1,/2), otherwise take first which is /2
            $pos = $1 eq $2 ? $-[1] : $-[2];
        }
        
	my $old_block = "";
	if ($pos > 0)
	{
	    $old_block = substr($$ref_string, 0, $pos, "");
	}

	# print STDERR Dumper({
	#     pos => $pos,
	#     old_block => $old_block,
	#     buffer => $$ref_string,
	# 	     }); use Data::Dumper;
    }

}

sub get_chunk
{
    my (
	$fh,
        $current_chunk,
	$output_fh,
	$fileformat,
	$skip
	) = @_;

    my $starting_pos = tell($fh);

    # after some discussion with Thomas we changed the estimation of
    # the chunksize2get each chunk begins at a multiple of the chunk
    # size and the limit is therefore the next chunk start
    my $next_chunk_pos = $current_chunk*$CHUNK_SIZE;

    if ($_debug)
    {
	printf STDERR "Starting file position: current %d (expected %d), next chunk starts at %d\n", $starting_pos, $current_chunk*$CHUNK_SIZE, $next_chunk_pos;
    }

    # we reduce the chunk size by 10 to find the right new block
    my $security_margin = 10;
    my $buffer = "";

    my $chunksize2get = $next_chunk_pos - $starting_pos - $security_margin;

    if($chunksize2get < 0){
        die "--chunk-number to high/--chunk-size to small for record!"
    }
    
    # read the number of chunksize2get bytes if skip is not set
    if ($skip == 0)
    {
	my $bytesread = read($fh, $buffer, $chunksize2get);
    } else {
        if(-p $fh){ # cannot seek on pipe
            my $idontcareabout;
            read($fh, $idontcareabout, $chunksize2get);
        }else{
            seek($fh, $next_chunk_pos, 0);
        }
	$cache = "";
	return;
    }

    # if the cache is set
    if ($cache)
    {
	$buffer = $cache.$buffer;
	$cache = "";
    }else{
        # this is only required after seeking, if cache exists, unless
        # interleaved it always trims 0, which is just more work than
        # necessary. On interleaved files it omits skip /2 records,
        # which are than lost completely
        strip_to_blockstart(\$buffer);
    }
    
    # print the buffer to output_fh if skip is not set
    if ($skip == 0 && $buffer)
    {
        print $output_fh $buffer;
    }

    # find the next block
    # read until the next line break
    $buffer = <$fh>;

    # minimum length of buffer have to be 10
    if ($buffer && length($buffer) < $security_margin)
    {
	# print the buffer to output_fh if skip is not set
	if ($skip == 0  && $buffer)
	{
	    print $output_fh $buffer;
	}
	# and get the next line
	$buffer = <$fh>;
    }

    # print the buffer to output_fh if skip is not set
    if ($skip == 0 && $buffer)
    {
	print $output_fh $buffer;
    }

    # find next block
    while (1)
    {
	$buffer = <$fh>;

	# check if we reached the eof and leave the loop in this case
	if (eof($fh))
	{
	    last;
	}

	# check if buffer contains a new block
	if ($fileformat eq "fasta" && $buffer =~ /^>/)
	{
	    # store the new block inside the cache variable
	    $cache = $buffer;
	    # and leave the loop
	    last;
	} elsif  ($fileformat eq "fastq" && $buffer =~ /^@/)
	{
	    # for fastq the next line have to be the sequence and the next line have to start with a plus
	    my $seq = <$fh>;
	    my $qhead = <$fh>;

	    if ($qhead =~ /^\+/)
	    {
                # okay... we reached a new block
                $buffer .= $seq.$qhead;

                if($interleaved){
                    # compare ids
                    $buffer .= <$fh>; # qual to buffer

                    # read second record for comparison
                    my $shead2 = <$fh>;
                                        
                    # handly possible eof
                    unless(defined($shead2)){
                        if ($skip == 0 && $buffer) {
                            print $output_fh $buffer;
                        }
                        last;
                    }

                    my $seq2 = <$fh>;
                    my $qhead2 = <$fh>;
                    my $qual2 = <$fh>;

                    my ($id1) = $buffer =~ /^@(\S+?)(?:\/[12])?\s/;
                    my ($id2) = $shead2 =~ /^@(\S+?)(?:\/[12])?\s/;

                    if ($id1 ne $id2) {
                        if ($skip == 0 && $buffer) {
                            print $output_fh $buffer;
                        }
                        $buffer = $shead2.$seq2.$qhead2.$qual2;
                    }else{
                        if ($skip == 0 && $buffer) {
                            print $output_fh $buffer, $shead2, $seq2, $qhead2, $qual2;
                        }
                        $buffer = "";
                    }
                }

                
	    } elsif ($seq =~ /^@/)
	    {
		# the block border was wrong and the start of a
		# quality line, therefore, the next line should be the
		# start of a new block write the buffer to the output
		# if required and fill the buffer with the right
		# content
		if ($skip == 0 && $buffer)
		{
                    print $output_fh $buffer;
		}

                $buffer = $seq.$qhead;
                                
                if($interleaved){
                    # compare ids
                    $buffer .= <$fh>.<$fh>; # qual to buffer

                    # read second record for comparison
                    my $shead2 = <$fh>;
                                        
                    # handly possible eof
                    unless(defined($shead2)){
                        if ($skip == 0 && $buffer) {
                            print $output_fh $buffer;
                        }
                        last;
                    }

                    my $seq2 = <$fh>;
                    my $qhead2 = <$fh>;
                    my $qual2 = <$fh>;

                    my ($id1) = $buffer =~ /^@(\S+?)(?:\/[12])?\s/;
                    my ($id2) = $shead2 =~ /^@(\S+?)(?:\/[12])?\s/;

                    if ($id1 ne $id2) {
                        if ($skip == 0 && $buffer) {
                            print $output_fh $buffer;
                        }
                        $buffer = $shead2.$seq2.$qhead2.$qual2;
                    }else{
                        if ($skip == 0 && $buffer) {
                            print $output_fh $buffer, $shead2, $seq2, $qhead2, $qual2;
                        }
                        $buffer = "";
                    }
                }
                                
	    } else {
		die "Unable to find a new block in a fastq file\n";
	    }

            
            
	    $cache = $buffer;
	    # and leave the loop
	    last;
	} else {
	    # not a new block so print the buffer to output_fh if skip is not set
	    if ($skip == 0  && $buffer)
	    {
		print $output_fh $buffer;
	    }
	}
    }
}




sub test_expand_byte_suffix
{
    foreach (qw(100 0001 15 10k 10K 10m 10M 10g 10G 5 9L4))
    {
	print STDERR expand_byte_suffix($_), "\n";
    }
}

sub test_guess_file_format
{
    foreach (qw(test.fasta test.fastq test.other))
    {
	open(my $fh, "<", $_) || die;
	my $fileformat=guess_file_format($fh) || die "The file seems to be neigher a FASTQ nor a FASTA file\n";
	print STDERR "$_ was detected as $fileformat\n";
	close($fh) || die;
    }
}

__END__
